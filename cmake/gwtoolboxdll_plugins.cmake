add_library(plugin_base INTERFACE)
target_sources(plugin_base INTERFACE
    "plugins/Base/dllmain.cpp"
    "plugins/Base/stl.h"
    "plugins/Base/BackupManager.h"
    "plugins/Base/BackupManager.cpp"
	"plugins/Base/FontLoader.h"
	"plugins/Base/FontLoader.cpp"
	"plugins/Base/toolbox_default_font.h"
	"plugins/Base/imgui_impl_dx9.h"
	"plugins/Base/imgui_impl_dx9.cpp"
    "plugins/Base/ToolboxPlugin.h"
    "plugins/Base/ToolboxPlugin.cpp"
    "plugins/Base/Pathing.h"
    "plugins/Base/Pathing.cpp"
    "plugins/Base/PluginUtils.h"
    "plugins/Base/PluginUtils.cpp"
    "plugins/Base/Rendering.h"
    "plugins/Base/Rendering.cpp"
    "plugins/Base/ToolboxUIPlugin.h"
    "plugins/Base/ToolboxUIPlugin.cpp")
target_include_directories(plugin_base INTERFACE
    "plugins/Base"
    "GWToolboxdll" # careful here, we only get access to exported and header functions!
    ${SIMPLEINI_INCLUDE_DIRS}
    )
target_link_libraries(plugin_base INTERFACE
    imgui
	imgui::fonts
    nlohmann_json::nlohmann_json
    gwca
	Microsoft::DirectXTex
	directxtexloader
    )
target_compile_definitions(plugin_base INTERFACE BUILD_DLL)

macro(add_tb_plugin PLUGIN)
    add_library(${PLUGIN} SHARED)
    file(GLOB SOURCES CONFIGURE_DEPENDS
        "${PROJECT_SOURCE_DIR}/plugins/${PLUGIN}/*.h"
        "${PROJECT_SOURCE_DIR}/plugins/${PLUGIN}/*.cpp")
    target_sources(${PLUGIN} PRIVATE ${SOURCES})
    target_include_directories(${PLUGIN} PRIVATE "${PROJECT_SOURCE_DIR}/plugins/${PLUGIN}")
    target_link_libraries(${PLUGIN} PRIVATE plugin_base)
    target_compile_options(${PLUGIN} PRIVATE /wd4201 /wd4505)
    target_compile_options(${PLUGIN} PRIVATE /W4 /WX /Gy)
    target_compile_options(${PLUGIN} PRIVATE $<$<NOT:$<CONFIG:Debug>>:/GL>)
    target_compile_options(${PLUGIN} PRIVATE $<$<CONFIG:Debug>:/ZI /Od>)
    target_link_options(${PLUGIN} PRIVATE /WX /OPT:REF /OPT:ICF /SAFESEH:NO)
    target_link_options(${PLUGIN} PRIVATE $<$<NOT:$<CONFIG:Debug>>:/LTCG /INCREMENTAL:NO>)
    target_link_options(${PLUGIN} PRIVATE $<$<CONFIG:Debug>:/IGNORE:4098 /OPT:NOREF /OPT:NOICF>)
    target_link_options(${PLUGIN} PRIVATE $<$<CONFIG:RelWithDebInfo>:/OPT:NOICF>)
    set_target_properties(${PLUGIN} PROPERTIES FOLDER "plugins/")
endmacro()

add_tb_plugin(AgentPopTimer)
add_tb_plugin(DeathPenaltyTimer)
add_tb_plugin(DhuumCalculator)
add_tb_plugin(DialogsWindow)
add_tb_plugin(ExamplePlugin)
add_tb_plugin(FlatBowRangeIndicator)
add_tb_plugin(Follow)
add_tb_plugin(LeechSignetCancel)
add_tb_plugin(PitsSoulsWindow)
add_tb_plugin(PathingVisualizer)
add_tb_plugin(ProjectileIndicator)
add_tb_plugin(RawDialogs)
add_tb_plugin(ShadowstepPredictor)
add_tb_plugin(SkinChanger)
add_tb_plugin(Slowload)
add_tb_plugin(TargetEverything)